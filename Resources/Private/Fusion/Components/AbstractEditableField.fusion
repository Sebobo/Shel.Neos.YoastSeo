prototype(Shel.Neos.YoastSeo:Component.AbstractEditableField) < prototype(Neos.Fusion:Tag) {
    @context {
        fieldName = 'changeme'
        fieldClass = 'changeme'
    }

    attributes {
        class = 'yoast-seo__field-wrap'
        class.@process.addFieldClass = ${value + ' yoast-seo__' + fieldClass + '-wrap'}
    }

    content = Neos.Fusion:Array {
        label = Neos.Fusion:Tag {
            tagName = 'label'
            content = ${I18n.translate('field.' + fieldName, null, [], 'Main', 'Shel.Neos.YoastSeo', null, interfaceLanguage)}
        }

        value = Neos.Fusion:Tag {
            tagName = 'div'
            attributes {
                class = 'yoast-seo__field--editable'
                class.@process.addFieldClass = ${value + ' yoast-seo__' + fieldClass}
            }
            content = ${q(documentNode).property(fieldName)}
            @process.contentElementEditableWrapping = Neos.Neos:ContentElementEditable {
                property = ${fieldName}
                node = ${documentNode}
            }
        }
    }
}
