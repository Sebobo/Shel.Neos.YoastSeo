prototype(Shel.Neos.YoastSeo:Documents.YoastSeoView) < prototype(Neos.Neos:Page) {
    head {
        stylesheets >
        stylesheets = Neos.Fusion:Array {
            main = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel = 'stylesheet'
                    type = 'text/css'
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Shel.Neos.YoastSeo/Public/Styles/main.css'
                    }
                }
            }
        }
    }

    bodyTag.attributes.class = 'neos-shel-yoast-seo-view-mode'

    body {
        templatePath = 'resource://Shel.Neos.YoastSeo/Private/Templates/YoastSeoView.html'
        sectionName = 'body'

        previewWrapper = Neos.Fusion:Tag {
            tagName = 'main'
            attributes.class = 'yoast-seo__wrapper'

            content = Neos.Fusion:Array {
                header = Neos.Fusion:Tag {
                    tagName = 'h1'
                    content = 'Yoast SEO analysis'
                }

                snippetPreview = Neos.Fusion:Tag {
                    attributes {
                        id = 'snippet'
                        class = 'yoast-seo__snippet'
                    }
                }

                previewUrl = Shel.Neos.YoastSeo:Component.PreviewUrl
                baseUrl = Shel.Neos.YoastSeo:Component.BaseUrl
                focusKeyword = Shel.Neos.YoastSeo:Component.FocusKeyword

                // Wrap editable fields for the current document node
                editableFieldsWrap = Neos.Fusion:Tag {
                    attributes {
                        id = 'editable-fields-wrap'
                        class = 'yoast-seo__editable-fields-wrap'
                    }
                    content = Neos.Fusion:Array {
                        seoTitle = Shel.Neos.YoastSeo:Component.SeoTitle
                        pageUriPathSegment = Shel.Neos.YoastSeo:Component.PageUriPathSegment
                        metaDescription = Shel.Neos.YoastSeo:Component.MetaDescription
                        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                            node = ${documentNode}
                        }
                    }
                }

                output = Neos.Fusion:Tag {
                    attributes {
                        id = 'output'
                        class = 'yoast-seo__output'
                    }
                }

                errorOutput = Neos.Fusion:Tag {
                    attributes {
                        id = 'errorOutput'
                        class = 'yoast-seo__errorOutput'
                    }
                }
            }
        }

        javascripts {
            app = Neos.Fusion:Tag {
                tagName = 'script'
                attributes {
                    async = 'async'
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://Shel.Neos.YoastSeo/Public/Scripts/Bundle.js'
                    }
                }
            }

            browserSync = Neos.Fusion:Template {
                templatePath = 'resource://Shel.Neos.YoastSeo/Private/Templates/YoastSeoView.html'
                sectionName = 'browserSync'
                @if.inDevMode = ${Configuration.setting('Shel.Neos.YoastSeo.devMode')}
            }
        }
    }
}
