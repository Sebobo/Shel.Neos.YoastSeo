prototype(Shel.Neos.YoastSeo:Documents.YoastSeoView) < prototype(Neos.Neos:Page) {

    @context.interfaceLanguage = Shel.Neos.YoastSeo:InterfaceLanguage

    head {
        stylesheets >
        stylesheets = Neos.Fusion:Array {
            main = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel = 'stylesheet'
                    type = 'text/css'
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Shel.Neos.YoastSeo/Public/Styles/main.css'
                    }
                }
            }
        }
    }

    bodyTag.attributes.class = 'neos-shel-yoast-seo-view-mode'

    body {
        templatePath = 'resource://Shel.Neos.YoastSeo/Private/Templates/YoastSeoView.html'
        sectionName = 'body'

        previewWrapper = Neos.Fusion:Tag {
            tagName = 'main'
            attributes.class = 'yoast-seo__wrapper'

            content = Neos.Fusion:Array {
                header = Shel.Neos.YoastSeo:Component.Header
                snippetPreview = Shel.Neos.YoastSeo:Component.SnippetPreview
                focusKeyword = Shel.Neos.YoastSeo:Component.FocusKeyword
                snippetFields = Shel.Neos.YoastSeo:Component.SnippetFields
                output = Shel.Neos.YoastSeo:Component.Output
                errorOutput = Shel.Neos.YoastSeo:Component.ErrorOutput

                configuration = Shel.Neos.YoastSeo:Component.Configuration
            }
        }

        javascripts {
            app = Neos.Fusion:Tag {
                tagName = 'script'
                attributes {
                    async = 'async'
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://Shel.Neos.YoastSeo/Public/Scripts/App.js'
                    }
                }
            }

            browserSync = Neos.Fusion:Template {
                templatePath = 'resource://Shel.Neos.YoastSeo/Private/Templates/YoastSeoView.html'
                sectionName = 'browserSync'
                @if.inDevMode = ${Configuration.setting('Shel.Neos.YoastSeo.devMode')}
            }
        }
    }
}
