prototype(Yoast.YoastSeoForNeos:Documents.YoastSeoView) < prototype(Neos.Neos:Page) {

    @context.interfaceLanguage = Yoast.YoastSeoForNeos:InterfaceLanguage

    head {
        stylesheets >
        stylesheets = Neos.Fusion:Array {
            main = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel = 'stylesheet'
                    type = 'text/css'
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/main.css'
                    }
                }
            }
        }
    }

    bodyTag.attributes.class = 'neos-yoast-seo-view-mode'

    body {
        templatePath = 'resource://Yoast.YoastSeoForNeos/Private/Templates/YoastSeoView.html'

        previewWrapper = Neos.Fusion:Tag {
            tagName = 'main'
            attributes.class = 'yoast-seo__container'

            content = Neos.Fusion:Array {
                header = Yoast.YoastSeoForNeos:Component.Header
                application = Yoast.YoastSeoForNeos:Component.Application
                modal = Neos.Fusion:Component {
                    renderer = afx`
                        <div id="yoast-modal"></div>
                    `
                }
                snippetEditor = Yoast.YoastSeoForNeos:Component.SnippetEditor
            }
        }

        javascripts {
            yoastseo = Neos.Fusion:Tag {
                tagName = 'script'
                attributes {
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/yoastseo.js'
                    }
                }
            }
            app = Neos.Fusion:Tag {
                tagName = 'script'
                attributes {
                    async = 'async'
                    src = Neos.Fusion:ResourceUri {
                        path = 'resource://Yoast.YoastSeoForNeos/Public/Assets/app.js'
                    }
                }
            }
        }
    }
}
